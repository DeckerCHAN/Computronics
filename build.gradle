buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://gregtech.overminddl1.com/"
		}

		maven {
			name = "GTNH"
			url = "http://downloads.gtnewhorizons.com/Mods_for_Jenkins/"
		}
		
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
		maven {
			url = "https://jitpack.io"
		}
	}
	dependencies {
		classpath 'com.github.GTNH2:ForgeGradle:FG_1.2-SNAPSHOT'
	}
}

apply plugin: 'forge'

file "build.properties" withReader {
	def prop = new Properties()
	prop.load(it)
	ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft.version}-${config.computronics.version}"
group = "pl.asie.computronics" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Computronics"

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

repositories {
	mavenCentral()
	jcenter() // Needed for Mary TTS
	maven {
		name = "OC repo"
		url = "http://maven.cil.li/"
	}
	maven {
		name = "Forestry and IC2 repo"
		url = "http://maven.ic2.player.to/"
	}
	maven {
		name = "Gendustry"
		url = "http://jenkins.bdew.net/maven"
	}
	maven {
		name = "GregTech"
		url = "http://gregtech.overminddl1.com/"
	}
	maven {
		name = "jitpack.io"
		url = "https://jitpack.io"
	}
	maven {
		name = "GTNH"
		url = "http://jenkins.usrv.eu:8081/nexus/content/repositories/releases/"
	}
	maven {
		name = "AppleCore"
		url = "http://www.ryanliptak.com/maven/"
	}
	ivy {
		name = "BuildCraft"
		artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-[classifier].[ext]"
	}
	ivy {
		name = 'gtnh_download_source_stupid_underscore_typo'
		artifactPattern "http://downloads.gtnewhorizons.com/Mods_for_Jenkins/[module]_[revision].[ext]"
	}
	ivy {
		name = 'gtnh_download_source'
		artifactPattern "http://downloads.gtnewhorizons.com/Mods_for_Jenkins/[module]-[revision].[ext]"
	}
	/*
	maven {
		name "Mobius Repo"
		url "http://mobiusstrip.eu/maven"
	}
	maven {
		name 'CB Repo'
		url "http://chickenbones.net/maven/"
	}
	maven {
		name 'tterrag Repo'
		url "http://maven.tterrag.com/"
	}
	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven/"
	}
	 */
}

configurations {
	embedded
	implementation.extendsFrom embedded
	all*.exclude group: "codechicken"
}

dependencies {
	implementation "li.cil.oc:OpenComputers:${config.oc.version}:dev"
	implementation "li.cil.tis3d:TIS-3D:${config.tis3d.version}:dev"
	compileOnly "net.industrial-craft:industrialcraft-2:${config.ic2.version}:dev"
	compileOnly "com.github.GTNewHorizons:GT5-Unofficial:${config.gt5.version}:dev"
	//compileOnly "com.gregoriust.gregtech:gregtech_1.7.10:${config.gregtech.version}:dev"
	compileOnly "com.gregoriust.gregtech:gregtech_1.7.10:${config.gregtech6.version}:dev"
	implementation "mcp.mobius.waila:Waila:${config.waila.version}:dev"
	implementation "com.enderio.core:EnderCore:${config.endercore.version}:dev"
	implementation("com.enderio:EnderIO:${config.enderio.version}:dev") {
		exclude module: "appliedenergistics2"
		exclude module: "buildcraft"
		exclude module: "Waila"
		exclude module: "forestry_1.7.10"
		exclude module: "OpenComputers"
		exclude module: "CodeChickenLib"
		exclude module: "CodeChickenCore"
		exclude module: "EquivalentExchange3"
		exclude module: "EnderCore"
	}
	implementation("net.sengir.forestry:forestry_1.7.10:${config.forestry.version}:dev") {
		exclude module: "buildcraft"
		exclude module: "industrialcraft-2"
	}
	compileOnly "net.bdew:gendustry:${config.gendustry.version}-mc${config.minecraft.version}:dev"
	implementation name: "buildcraft", version: "${config.buildcraft.version}", classifier: "dev"
	compileOnly "mods.railcraft:Railcraft:${config.minecraft.version}-${config.railcraft.version}"
	implementation("appeng:appliedenergistics2:${config.ae2.version}:dev") {
		transitive = false
	}
	//compileOnly "curse.maven:armourers-workshop:${config.am.version}"
	implementation("de.dfki.mary:voice-cmu-slt-hsmm:${config.marytts.version}") {
		exclude module: "commons-io"
	}
	//compileOnly("com.bluepowermod:BluePower:${config.bluepower.version}:deobf") {
	//	transitive = false
	//}
	//compileOnly("qmunity:QmunityLib:${config.qmunitylib.version}:deobf") {
	//	transitive = false
	//}
}

minecraft {
	version = "${config.minecraft.version}-${config.forge.version}"
	runDir = "eclipse"

	replaceIn "pl/asie/computronics/Computronics.java"
	replace "@VERSION@", "${config.computronics.version}"
}

sourceSets {
	main {
		output.resourcesDir = 'build/classes/java/main'
	}
}

apply plugin: 'idea'
idea {
	module {
		outputDir = file('build/classes/java/main')
	}
}

processResources {
	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		// replace version and mcversion
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	// Move access transformer to META-INF
	rename '(.+_at.cfg)', 'META-INF/$1'
}

def commonManifest = {
	attributes FMLAT: "tts_at.cfg"
}

//For Building

jar {
	exclude 'cofh/**'
	manifest commonManifest
}

def libDir = './build/libs'

task deobfJar(type: Jar) {
	description = 'Creates a jar file containing the non-obfuscated class files'
	classifier = 'deobf'
	destinationDir = file(libDir)
	from sourceSets.main.output.classesDirs
	exclude 'cofh/**'
	manifest commonManifest
}

task apiZip(type: Zip) {
	description = 'Creates a zip file containing the API source files only'
	classifier = 'api'
	destinationDir = file(libDir)
	from sourceSets.main.allSource
	include 'pl/asie/**/api/**'
}

task makeJars {
	description = 'Creates the mod files'
}

makeJars.dependsOn clean, build, deobfJar, apiZip
build.shouldRunAfter clean
deobfJar.shouldRunAfter build
apiZip.shouldRunAfter deobfJar
